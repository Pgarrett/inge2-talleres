plugins {
    id 'java'
    id 'info.solidsoft.pitest' version '1.9.0'
    id 'jacoco'
}

group 'org.autotest'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    implementation 'com.github.randoop:randoop:4.3.2'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

pitest {
    //adds dependency to org.pitest:pitest-junit5-plugin and sets "testPlugin" to "junit5"
    junit5PluginVersion = '1.0.0'

    // Check "Plugin configuration" in https://gradle-pitest-plugin.solidsoft.info/ for available settings.
    timestampedReports = false
}

task fixTestsImport() {
    group = "help"
    description = "Fixes the import statements in the tests files generated by Randoop so we can run tests using Junit 5."

    doLast {
        fileTree(dir: 'src/test/java', include: '**/*.java').each { file ->
            file.text = file.text.replace('org.junit.Test;', 'org.junit.jupiter.api.Test;')
        }
    }
}

task randoop(type: JavaExec, dependsOn: jar) {
    group = "verification"
    description = "Runs Randoop on the project's classes."

    main = "randoop.main.Main"
    classpath = sourceSets.main.runtimeClasspath

    args('gentests')
    args('--testclass', 'org.autotest.StackAr')
    args('--time-limit', '60') // seconds
    args('--testsperfile', '250')
    args('--junit-output-dir', 'src/test/java')
    args('--junit-package-name', 'org.autotest')

    finalizedBy fixTestsImport
}